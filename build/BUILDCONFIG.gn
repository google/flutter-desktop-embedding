# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

default_static_configs = [ "//build:defaults" ]

default_dynamic_configs = [
  "//build:defaults",
  "//build:shared_library_defaults",
]

set_defaults("executable") {
  configs = default_static_configs
}
set_defaults("static_library") {
  configs = default_static_configs
}
set_defaults("source_set") {
  configs = default_static_configs
}
set_defaults("shared_library") {
  configs = default_dynamic_configs
}
set_defaults("published_shared_library") {
  configs = default_dynamic_configs
}

if (host_os == "linux") {
  set_default_toolchain("//third_party/chromium/build/toolchain:gcc")
  is_linux = true
  is_mac = false
  is_win = false
} else if (host_os == "mac") {
  is_linux = false
  is_mac = true
  is_win = false
} else if (host_os == "win") {
  set_default_toolchain("//build/win/toolchain:msvc")
  is_linux = false
  is_mac = false
  is_win = true
}

declare_args() {
  # Whether to use the GLFW embedding implementation. Currently overriding this
  # would not work since each platform only has one working implementation,
  # but in the future GLFW may be one of multiple supported implementations.
  use_glfw = is_linux || is_win
}
