# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Puts the public headers of the invoker in public_header_subdir
# under the top-level include/ directory in the build output.
#
# This is intended to make consuming public headers of all the libraries built
# by GN easy for outside build systems, by requiring only a single include
# directory.
template("copy_includes") {
  copy(target_name) {
    assert(defined(invoker.sources),
           "|sources| must be provided for copy_includes.")
    forward_variables_from(invoker,
                           [
                             "deps",
                             "sources",
                             "subdir",
                           ])
    output_dir = "$root_out_dir/include"
    if (defined(subdir)) {
      output_dir = "$output_dir/$subdir"
    }
    outputs = [
      "$output_dir/{{source_file_part}}",
    ]
  }
}

# An abstraction for a shared library with associated headers that is intended
# to be consumed from the build output.
#
# This performs a copy_includes in addition to building the library.
template("published_shared_library") {
  template_target_name = target_name

  if (is_linux || is_win) {
    copy_includes("_publish_${template_target_name}_headers") {
      sources = invoker.public
      subdir = invoker.public_header_subdir
    }

    shared_library(template_target_name) {
      forward_variables_from(invoker, "*")
      assert(defined(public),
             "|public| must be provided for published_shared_library.")

      if (!defined(deps)) {
        deps = []
      }
      deps += [ ":_publish_${template_target_name}_headers" ]
    }
  }
}
