# Copyright 2019 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

_wrapper_includes = [
  "include/flutter_desktop_embedding/basic_message_channel.h",
  "include/flutter_desktop_embedding/binary_messenger.h",
  "include/flutter_desktop_embedding/engine_method_result.h",
  "include/flutter_desktop_embedding/json_message_codec.h",
  "include/flutter_desktop_embedding/json_method_codec.h",
  "include/flutter_desktop_embedding/message_codec.h",
  "include/flutter_desktop_embedding/method_call.h",
  "include/flutter_desktop_embedding/method_channel.h",
  "include/flutter_desktop_embedding/method_codec.h",
  "include/flutter_desktop_embedding/method_result.h",
  "include/flutter_desktop_embedding/plugin_registrar.h",
]
_wrapper_sources = [
  "src/engine_method_result.cc",
  "src/json_message_codec.cc",
  "src/json_method_codec.cc",
  "src/plugin_handler.cc",
  "src/plugin_handler.h",
]

if (use_glfw) {
  _wrapper_includes +=
      [ "include/flutter_desktop_embedding/glfw/flutter_window_controller.h" ]
  _wrapper_sources += [ "src/glfw/flutter_window_controller.cc" ]
}

_wrapper_publish_dir = "$root_out_dir/fde_cpp_wrapper"

# A static library version of the client wrapper, for use both within the
# framework implementation itself, as well as with the plugin builds.
source_set("client_wrapper") {
  sources = _wrapper_sources
  public = _wrapper_includes

  if (is_linux) {
    configs = [
      "//build/linux/config:jsoncpp",

      # Use shared library settings since this target will be folded into
      # shared libraries.
      "//build:shared_library_defaults",
    ]
  }
  if (is_win) {
    deps = [
      "//third_party/jsoncpp:jsoncpp",
    ]
  }

  public_configs = [ ":_relative_headers" ]

  # Since these sources are for client use, they are written assuming they will
  # consume the embedder as a framework.
  include_dirs = [ "//library/include/" ]

  # Explicitly disable flattened headers, since this is being built in-tree.
  # TODO: Simplify down to one define once Windows is using GN.
  defines = [ "USE_FLATTENED_INCLUDES=0", "USE_FDE_TREE_PATHS" ]
}

# Since this code is intended to be used by clients in a source bundle, the
# include paths use library-relative header paths rather than project-relative.
config("_relative_headers") {
  include_dirs = [ "include" ]
}

copy("_publish_includes") {
  sources = _wrapper_includes
  output_dir = "$_wrapper_publish_dir/include/flutter_desktop_embedding"
  outputs = [
    "$output_dir/{{source_file_part}}",
  ]
}

copy("_publish_sources") {
  sources = _wrapper_sources + [ "README" ]
  output_dir = "$_wrapper_publish_dir"
  outputs = [
    "$output_dir/{{source_file_part}}",
  ]
}

group("publish_wrapper") {
  deps = [
    ":_publish_includes",
    ":_publish_sources",
  ]
}
